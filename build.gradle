plugins {
    id 'org.springframework.boot' version '2.4.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}

project.ext {
    publishRepo = "https://maven.pkg.github.com/reportportal/service-jobs"
    releaseMode = project.hasProperty("releaseMode")
}

def scriptsUrl = 'https://raw.githubusercontent.com/reportportal/gradle-scripts/' + (releaseMode ? getProperty('scripts.version') : '5.5.0')

apply from: "$scriptsUrl/build-docker.gradle"
apply from: "$scriptsUrl/build-commons.gradle"
apply from: "$scriptsUrl/build-info.gradle"
apply from: "$scriptsUrl/build-quality.gradle"
apply from: "$scriptsUrl/release-service.gradle"
apply from: "$scriptsUrl/signing.gradle"

sourceCompatibility = '11'

wrapper {
    gradleVersion = '6.3'
}

bootJar {
    project.hasProperty('gcp') ? getArchiveFileName().set('app.jar') : archiveClassifier.set('exec')
}
jar.enabled(true)

repositories {
    mavenCentral { url "https://repo1.maven.org/maven2" }
    maven { url 'https://repo.spring.io/libs-milestone' }
}

processResources {
    dependsOn(gitInfo)
    filesMatching('application.properties') {
        expand(project.properties)
    }
}

dependencies {
    implementation 'net.javacrumbs.shedlock:shedlock-spring:4.21.0'
    implementation 'net.javacrumbs.shedlock:shedlock-provider-jdbc-template:4.21.0'

    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'


    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation 'io.minio:minio:6.0.13'

    compile "com.rabbitmq:http-client:2.1.0.RELEASE"

    runtimeOnly 'org.postgresql:postgresql'

    testCompile 'org.junit.jupiter:junit-jupiter:5.5.2'
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    testCompile 'org.junit.jupiter:junit-jupiter-params:5.5.2'
    testCompile 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
    testCompile 'org.mockito:mockito-junit-jupiter:3.1.0'

}

test {
    useJUnitPlatform()
}

addDockerfileToGit.dependsOn createDockerfile
beforeReleaseBuild.dependsOn addDockerfileToGit
publish.dependsOn build
publish.mustRunAfter build

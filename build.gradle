plugins {
    id 'org.springframework.boot' version '2.7.16'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}

project.ext {
    publishRepo = "https://maven.pkg.github.com/reportportal/service-jobs"
    releaseMode = project.hasProperty("releaseMode")
}

def scriptsUrl = 'https://raw.githubusercontent.com/reportportal/gradle-scripts/' + (releaseMode ? '5.10.0' : 'EPMRPP-85756')

apply from: "$scriptsUrl/build-docker.gradle"
apply from: "$scriptsUrl/build-commons.gradle"
apply from: "$scriptsUrl/build-info.gradle"
//apply from: "$scriptsUrl/build-quality.gradle"
apply from: "$scriptsUrl/release-service.gradle"
apply from: "$scriptsUrl/signing.gradle"

sourceCompatibility = '21'

wrapper {
    gradleVersion = '8.4'
}

bootJar {
    project.hasProperty('gcp') ? getArchiveFileName().set('app.jar') : archiveClassifier.set('exec')
}
jar.enabled(true)
jar.archiveClassifier.set('')

repositories {
    mavenCentral { url "https://repo1.maven.org/maven2" }
    maven { url 'https://repo.spring.io/libs-milestone' }
    maven { url 'https://jitpack.io' }
}

processResources {
    dependsOn(gitInfo)
    filesMatching('application.properties') {
        expand(project.properties)
    }
}

//https://nvd.nist.gov/vuln/detail/CVE-2020-9488 and https://nvd.nist.gov/vuln/detail/CVE-2021-44228 and https://nvd.nist.gov/vuln/detail/CVE-2021-45046 and
//https://nvd.nist.gov/vuln/detail/CVE-2021-45105
ext['log4j2.version'] = '2.17.1'
ext['log4j-to-slf4j.version'] = '2.17.1'
//https://nvd.nist.gov/vuln/detail/CVE-2022-26520
ext['postgresql.version'] = '42.4.1'
ext['snakeyaml.version'] = '1.31'
//

dependencies {

    implementation group: 'org.json', name: 'json', version: '20220320'

    implementation 'net.javacrumbs.shedlock:shedlock-spring:4.21.0'
    implementation 'net.javacrumbs.shedlock:shedlock-provider-jdbc-template:4.21.0'

    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'


    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation 'org.apache.jclouds.api:s3:2.5.0'
    implementation 'org.apache.jclouds.provider:aws-s3:2.5.0'
    //Needed for correct jcloud work
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'org.apache.httpcomponents:httpclient:4.5.13'
//    https://avd.aquasec.com/nvd/cve-2020-8908
//    implementation 'com.google.guava:guava:30.0-jre';

    implementation "com.rabbitmq:http-client:2.1.0.RELEASE"
    //Fix CVE
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.4.2'

    runtimeOnly 'org.postgresql:postgresql'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.5.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.5.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.1.0'

}

test {
    useJUnitPlatform()
}

publish.dependsOn build
publish.mustRunAfter build
